1000 REM AM9511.BAS
1010 REM
1020 REM INTERFACE WITH AM9511 CHIP (EMULATOR)
1030 REM THIS IS *NOT* PRODUCTION CODE -- JUST PROOF OF CONCEPT
1040 REM THIS SHOULD BE ENOUGH TO WRITE A TEST SEQUENCE FOR BOTH
1050 REM EMULATOR AND ACTUAL CHIP
1060 REM
1070 GOTO 1130
1080 GOSUB 1660 ' AM9511 -> X
1090 GOSUB 1430 ' X -> AM9511
1100 GOSUB 1370 ' WAIT FOR NOT BUSY
1110 GOSUB 1580 ' PUSH 0D/0.0
1120 GOSUB 1620 ' PUSH 0S
1130 AM.STATUS = &H51
1140 AM.DATA = &H50
1150 X = 0!
1160 REM
1170 REM MAIN
1180 GOSUB 1370
1190 OUT AM.STATUS,&H0 ' NOP
1200 GOSUB 1370
1210 S = INP(AM.STATUS) : PRINT "NOP STATUS = "; HEX$(S)
1220 OUT AM.STATUS,&H1A ' PUPI
1230 GOSUB 1370
1240 S = INP(AM.STATUS) : PRINT "PUPI STATUS = "; HEX$(S)
1250 GOSUB 1660 : PRINT "   "; X
1260 Y=X ' TRY SENDING PI BACK TO CHIP AND TESTING
1270 REM WE CAN TRY TO "ROUND-TRIP" PI. WE USED PUPI TO GET THE
1280 REM VALUE INTO THE CHIP, THEN READ IT OUT, AND CONVERTED TO MS.
1290 REM WE SEND THAT VALUE BACK TO THE CHIP, THEN READ IT AGAIN
1300 REM THE VALUE OF "PUPI" CAN BE CONVERTED TO AND FROM MS WITH
1310 REM NO LOSS.
1320 GOSUB 1430
1330 GOSUB 1660 : PRINT Y, X
1331 X=10.5 : Z=3.1 : PRINT "BEGIN"
1332 FOR I=1 TO 10000:Z=X*Y:NEXT
1333 PRINT "END"
1340 STOP
1350 REM WAIT FOR AM9511 NOT BUSY
1360 REM
1370 WAIT AM.STATUS,&H80,&H80
1380 RETURN
1390 REM PUSH MS FLOAT TO AM9511 STACK
1400 REM 
1410 REM INCOMING FLOAT IS IN VARIABLE X
1420 REM
1430 P = VARPTR(X)
1440 IF PEEK(P + 3) = 0 THEN 1580
1450 S = PEEK(P + 2) AND &H80
1460 E = PEEK(P + 3) - 128
1470 M.H = PEEK(P + 2) OR &H80
1480 M.LL = PEEK(P + 0)
1490 M.LH = PEEK(P + 1)
1500 E = (E AND &H7F) OR S
1510 OUT AM.DATA,M.LL
1520 OUT AM.DATA,M.LH
1530 OUT AM.DATA,M.H
1540 OUT AM.DATA,E
1550 RETURN
1560 REM
1570 REM WRITE 00 00 00 00 (ZERO) TO CHIP
1580 OUT AM.DATA,0
1590 OUT AM.DATA,0
1600 REM
1610 REM WRITE 0 (ZERO) TO CHIP
1620 OUT AM.DATA,0
1630 OUT AM.DATA,0
1640 RETURN
1650 REM POP FLOAT FROM AM9511 STACK, RETURN IN X
1660 B3 = INP(AM.DATA)
1670 B2 = INP(AM.DATA)
1680 B1 = INP(AM.DATA)
1690 B0 = INP(AM.DATA)
1700 REM PRINT HEX$(B0),HEX$(B1),HEX$(B2),HEX$(B3)
1710 X = 0
1720 IF (B2 AND &H80) = 0 THEN RETURN
1730 S = B3 AND &H80
1740 E = B3 AND &H7F
1750 M.H = B2
1760 M.LL = B0
1770 M.LH = B1
1780 E = E + 128
1790 M.H = M.H AND &H7F
1800 M.H = M.H OR S
1810 P = VARPTR(X)
1820 POKE P + 0,M.LL
1830 POKE P + 1,M.LH
1840 POKE P + 2,M.H
1850 POKE P + 3,E
1860 RETURN
M.H OR S
1810 P = VARPTR(X)
18
